DeviceRegistryEvents 
| where InitiatingProcessFileName == "explorer.exe"
| where ActionType == "RegistryValueSet"
| where RegistryKey has  @"\Software\Microsoft\Windows\CurrentVersion\Explorer\RunMRU"
| where tolower(RegistryValueData) has "powershell"
| project RUNMRUTimestamp=Timestamp,DeviceId,InitiatingProcessAccountName,DeviceName,RegistryValueName,RegistryValueData,InitiatingProcessFileName,
InitiatingProcessCommandLine
//optinional
| join kind=inner (
    DeviceProcessEvents 
    | where InitiatingProcessFileName == "explorer.exe"
    | where FileName == "powershell.exe"
    | where ProcessCommandLine has_any ("Net.WebClient","DownloadFile","Invoke-WebRequest","iwr","Invoke-Shellcode","http:",
    "New-Object System.Net.Sockets.TcpClient","IEX","Invoke-Expression","DownloadString","base64","-EcondedCommand","-enc")
    | project PWSHTimestamp=Timestamp,DeviceId,DeviceName,ActionType,User=AccountName
) on DeviceName
| where RUNMRUTimestamp  between (PWSHTimestamp .. 5m)
| project RUNMRUTimestamp,PWSHTimestamp,User,DeviceName,RegistryValueName,RegistryValueData,InitiatingProcessCommandLine
